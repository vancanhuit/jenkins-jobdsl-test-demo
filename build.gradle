plugins {
    id 'groovy'
}

ext {
    jenkinsVersion = '2.263.4'
    jobDslVersion = '1.77'
}

sourceSets {
    jobs {
        groovy {
            srcDirs 'jobs'
            compileClasspath += main.compileClasspath
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://repo.jenkins-ci.org/public/'
    }
}

configurations {
    testImplementation {
        exclude group: 'xalan'
        exclude group: 'xerces'
    }
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.5.13'
    implementation "org.jenkins-ci.plugins:job-dsl-core:${jobDslVersion}"

    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'junit:junit:4.13'

    testImplementation 'org.jenkins-ci.main:jenkins-test-harness:2.72'
    testImplementation "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"
    testImplementation "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testImplementation 'org.jenkins-ci.plugins:structs:1.22@jar'
    testImplementation 'org.jenkins-ci.plugins:script-security:1.76@jar'
}
